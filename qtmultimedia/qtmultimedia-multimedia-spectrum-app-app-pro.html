<?xml version="1.0" encoding="UTF-8"?>
<!DOCTYPE html>
<html lang="en">
<head>
  <meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
  <title>app.pro Example File | Qt Multimedia 5.7</title>
  <link rel="stylesheet" type="text/css" href="style/offline-simple.css" />
  <script type="text/javascript">
    window.onload = function(){document.getElementsByTagName("link").item(0).setAttribute("href", "style/offline.css");};
  </script>
</head>
<body>
<div class="header" id="qtdocheader">
  <div class="main">
    <div class="main-rounded">
      <div class="navigationbar">
        <table><tr>
<td ><a href="../qtdoc/supported-platforms-and-configurations.html#qt-5-7">Qt 5.7</a></td><td ><a href="qtmultimedia-index.html">Qt Multimedia</a></td><td ><a href="qtmultimedia-multimedia-spectrum-example.html">Spectrum Example</a></td><td >app.pro Example File</td></tr></table><table class="buildversion"><tr>
<td id="buildversion" width="100%" align="right">Qt 5.7.0 Reference Documentation</td>
        </tr></table>
      </div>
    </div>
<div class="content">
<div class="line">
<div class="content mainContent">
<div class="sidebar"><div class="sidebar-content" id="sidebar-content"></div></div>
<h1 class="title">app.pro Example File</h1>
<span class="subtitle">multimedia/spectrum/app/app.pro</span>
<!-- $$$multimedia/spectrum/app/app.pro-description -->
<div class="descr"> <a name="details"></a>
<pre class="cpp">

  include(../spectrum.pri)

  TEMPLATE = app

  TARGET = spectrum

  QT       += multimedia widgets

  SOURCES  += main.cpp \
              engine.cpp \
              frequencyspectrum.cpp \
              levelmeter.cpp \
              mainwidget.cpp \
              progressbar.cpp \
              settingsdialog.cpp \
              spectrograph.cpp \
              spectrumanalyser.cpp \
              tonegenerator.cpp \
              tonegeneratordialog.cpp \
              utils.cpp \
              waveform.cpp \
              wavfile.cpp

  HEADERS  += engine.h \
              frequencyspectrum.h \
              levelmeter.h \
              mainwidget.h \
              progressbar.h \
              settingsdialog.h \
              spectrograph.h \
              spectrum.h \
              spectrumanalyser.h \
              tonegenerator.h \
              tonegeneratordialog.h \
              utils.h \
              waveform.h \
              wavfile.h

  fftreal_dir = ../3rdparty/fftreal

  INCLUDEPATH += $${fftreal_dir}

  RESOURCES = spectrum.qrc

  # Dynamic linkage against FFTReal DLL
  !contains(DEFINES, DISABLE_FFT) {
      macx {
          # Link to fftreal framework
          LIBS += -F$${fftreal_dir}
          LIBS += -framework fftreal
      } else {
          LIBS += -L..$${spectrum_build_dir}
          LIBS += -lfftreal
      }
  }

  target.path = $$[QT_INSTALL_EXAMPLES]/multimedia/spectrum
  INSTALLS += target

  CONFIG += install_ok  # Do not cargo-cult this!

  # Deployment

  DESTDIR = ..$${spectrum_build_dir}
  macx {
      !contains(DEFINES, DISABLE_FFT) {
          # Relocate fftreal.framework into spectrum.app bundle
          framework_dir = ../spectrum.app/Contents/Frameworks
          framework_name = fftreal.framework/Versions/1/fftreal
          QMAKE_POST_LINK = \
              mkdir -p $${framework_dir} &amp;&amp;\
              rm -rf $${framework_dir}/fftreal.framework &amp;&amp;\
              cp -R $${fftreal_dir}/fftreal.framework $${framework_dir} &amp;&amp;\
              install_name_tool -id @executable_path/../Frameworks/$${framework_name} \
                                  $${framework_dir}/$${framework_name} &amp;&amp;\
              install_name_tool -change $${framework_name} \
                                  @executable_path/../Frameworks/$${framework_name} \
                                  ../spectrum.app/Contents/MacOS/spectrum
      }
  } else {
      linux-g++*: {
          # Provide relative path from application to fftreal library
          QMAKE_LFLAGS += -Wl,--rpath=\\\$\$ORIGIN
      }
  }

</pre>
</div>
<!-- @@@multimedia/spectrum/app/app.pro -->
        </div>
       </div>
   </div>
   </div>
</div>
<div class="footer">
   <p>
   <acronym title="Copyright">&copy;</acronym> 2016 The Qt Company Ltd.
   Documentation contributions included herein are the copyrights of
   their respective owners.<br>    The documentation provided herein is licensed under the terms of the    <a href="http://www.gnu.org/licenses/fdl.html">GNU Free Documentation    License version 1.3</a> as published by the Free Software Foundation.<br>    Qt and respective logos are trademarks of The Qt Company Ltd.     in Finland and/or other countries worldwide. All other trademarks are property
   of their respective owners. </p>
</div>
</body>
</html>
